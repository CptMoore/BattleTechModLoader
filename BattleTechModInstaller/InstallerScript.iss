; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!
; copied from https://github.com/Qwiso/QModManager/blob/master/Installer/QModsInstallerScript.iss

#define MyAppName "BattleTechMod Tools With Mods Preview"
#define MySetupName "BTMTWithModsPreviewSetup"
#define MyAppVersion "0.1.0"
#define MyAppPublisher "CptMoore"
#define MyAppURL "https://github.com/CptMoore/BattleTechModTools"
#define MyParentApp "BattleTech"
#define MyParentAppExe "BattleTech.exe"
#define MyParentAppSteamDirName "BattleTech"
; please adjust the code to your app

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{1765276C-3F2D-49BE-BB05-926C0BA2C863}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={code:GetDefaultDir}
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
OutputDir=.
OutputBaseFilename={#MySetupName}
SetupIconFile=Icon.ico
Compression=lzma
SolidCompression=yes
WizardImageFile=WizardImage.bmp
WizardSmallImageFile=WizardSmallImage.bmp
DisableWelcomePage=no
DisableDirPage=no
DirExistsWarning=no
UsePreviousAppDir=no

[Messages]
ExitSetupMessage=Setup is not complete. If you exit now, {#MyAppName} will not be installed.%n%nExit Setup?
SelectDirLabel3=Please select your {#MyParentApp} install folder.
SelectDirBrowseLabel=If this is correct, click Next. If you need to select a different {#MyParentApp} install folder, click Browse.
WizardSelectDir=Select {#MyParentApp} Install Location

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"
Name: "modsicon"; Description: "Add shortcut to mods folder"; GroupDescription: "{cm:AdditionalIcons}"

[Files]
Source: "..\BattleTechModLauncher\bin\Release\BattleTechModLauncher.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\BattleTechModLoaderInjector\bin\Release\0Harmony.dll"; DestDir: "{app}\BattleTech_Data\Managed"; Flags: ignoreversion
Source: "..\BattleTechModLoaderInjector\bin\Release\BattleTechModLoader.dll"; DestDir: "{app}\BattleTech_Data\Managed"; Flags: ignoreversion
Source: "..\BattleTechModLoaderInjector\bin\Release\BattleTechModLoaderInjector.exe"; DestDir: "{app}\BattleTech_Data\Managed"; Flags: ignoreversion
Source: "..\DynModLib\source\bin\Release\DynModLib.dll"; DestDir: "{app}\Mods\DynModLib"; Flags: ignoreversion
Source: "..\DynModLib\Mono.CSharp.dll"; DestDir: "{app}\Mods\DynModLib"; Flags: ignoreversion
Source: "..\DynModLib\mod.json"; DestDir: "{app}\Mods\DynModLib"; Flags: ignoreversion
Source: "..\ModTek\ModTek\bin\Release\ModTek.dll"; DestDir: "{app}\Mods"; Flags: ignoreversion
Source: "mods\SpeedMod\*"; DestDir: "{app}\Mods\SpeedMod"; Flags: ignoreversion recursesubdirs
Source: "mods\HardpointFixMod\*"; DestDir: "{app}\Mods\HardpointFixMod"; Flags: ignoreversion recursesubdirs
Source: "mods\StatsFixMod\*"; DestDir: "{app}\Mods\StatsFixMod"; Flags: ignoreversion recursesubdirs

[Icons]
Name: "{userdesktop}\BTMLauncher"; Filename: "{app}\BattleTechModLauncher.exe"; Tasks: desktopicon
Name: "{userdesktop}\BattleTech Mods"; Filename: "{app}\Mods\"; Tasks: modsicon

[Run]
Filename: "{app}\BattleTech_Data\Managed\BattleTechModLoaderInjector.exe"; Parameters: "/nokeypress"

[UninstallRun]
Filename: "{app}\BattleTech_Data\Managed\BattleTechModLoaderInjector.exe"; Parameters: "/restore /nokeypress"

[Code]
function GetDefaultDir(def: string): string;
var
I : Integer;
P : Integer;
steamInstallPath : string;
gameInstallPath : string;
configFile : string;
fileLines: TArrayOfString;
begin
	steamInstallPath := 'not found';
	if RegQueryStringValue( HKEY_LOCAL_MACHINE, 'SOFTWARE\WOW6432Node\Valve\Steam', 'InstallPath', steamInstallPath ) then
	begin
	end;

	if FileExists(steamInstallPath + '\steamapps\common\{#MyParentAppSteamDirName}\{#MyParentAppExe}') then
	begin
		Result := steamInstallPath + '\steamapps\common\{#MyParentAppSteamDirName}';
		Exit;
	end
	else
	begin
		configFile := steamInstallPath + '\config\config.vdf';
		if FileExists(configFile) then
		begin
			if LoadStringsFromFile(configFile, FileLines) then
			begin
				for I := 0 to GetArrayLength(FileLines) - 1 do
				begin
					P := Pos('BaseInstallFolder_', FileLines[I]);
					if P > 0 then
					begin
						steamInstallPath := Copy(FileLines[I], P + 23, Length(FileLines[i]) - P - 23);
						if FileExists(steamInstallPath + '\steamapps\common\{#MyParentAppSteamDirName}\{#MyParentAppExe}') then
						begin
							Result := steamInstallPath + '\steamapps\common\{#MyParentAppSteamDirName}';
							Exit;
						end;
					end;
				end;
			end;
		end;
  end
  
  gameInstallPath := 'not found'
  if RegQueryStringValue( HKEY_LOCAL_MACHINE, 'SOFTWARE\WOW6432Node\Microsoft\Windows\CurrentVersion\Uninstall\1482783682_is1', 'InstallLocation', gameInstallPath ) then
  begin
    Result := gameInstallPath
    Exit;
  end;
	
	Result := 'not found';
end;